{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Project 01: Campust Recruitment Data Analysis\n",
    "## SAS programs and outputs\n",
    "### 01. Data Exploration"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Create library name project */\n",
    "libname project '/folders/myfolders/Project/placement';\n",
    "\n",
    "/* Import dataset */\n",
    "proc import \n",
    "\t\tdatafile='/folders/myfolders/Project/placement/placement_data.csv' \n",
    "\t\tout=project.placement dbms=csv replace;\n",
    "run;\n",
    "\n",
    "/* Check charicteristics of dataset and confirm the dataset correctly read */\n",
    "proc contents data=project.placement;\n",
    "run;\n",
    "\n",
    "proc print data=project.placement noobs;\n",
    "\ttitle \"Campus Recruitment\";\n",
    "run;\n",
    "\n",
    "/* Check frequencies of categorical variables in dataset */\n",
    "proc freq data=project.placement nlevels order=freq;\n",
    "\ttable gender ssc_b hsc_b hsc_s workexp degree_t specialisation status;\n",
    "\ttitle \"Frequencies of Categorical Variables\";\n",
    "run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 02. Graphical Analysis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Identify associations in ANOVA with boxplots */\n",
    "/* Between salary and other categorcial variables */\n",
    "/* macro for graphical analysis connecting means */\n",
    "/* Part A */\n",
    "\n",
    "%let categorical = gender hsc_s degree_t workexp specialisation;\n",
    "\n",
    "%macro box(response=, regressor=);\n",
    "\n",
    "%let i = 1;\n",
    "\n",
    "%do %while(%scan(&regressor,&i,%str( )) ^= %str());\n",
    "\n",
    "    %let var = %scan(&regressor,&i,%str( ));\n",
    "    \n",
    "\tproc sgplot data = project.placement;\n",
    "\t\twhere &response ne .;\n",
    "\t\tvbox &response / category = &var connect = mean;\n",
    "\t\ttitle \"&response differences across &var\";\n",
    "    \n",
    "    %let i = %eval(&i + 1);\n",
    "\n",
    "%end;\n",
    "\n",
    "run;\n",
    "quit;\n",
    "title;\n",
    "\n",
    "%mend box;\n",
    "%box(response=salary, regressor=&categorical);\n",
    "\n",
    "\n",
    "/*identify associations in linear regression with scatter plots */\n",
    "/* between salary and other continuous variables */\n",
    "/* macro for graphical analysis */\n",
    "/* Part B */\n",
    "\n",
    "proc sgscatter data=project.placement;\n",
    "\twhere salary ne .;\n",
    "\tplot salary*mba_p / reg;\n",
    "\ttitle \"Association of MBA % with Salary\";\n",
    "run;\n",
    "title;\n",
    "\n",
    "\n",
    "%let factor2 = ssc_p hsc_p degree_P ptest_p mba_p;\n",
    "\n",
    "proc sgscatter data=project.placement;\n",
    "\twhere salary ne .;\n",
    "\tplot salary*(&factor2) / reg;\n",
    "\ttitle \"Association of Continuous Variables with Salary\";\n",
    "run;\n",
    "title;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 03. One-Way ANOVA"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Performing a one-way ANOVA to identify the differences */\n",
    "/* between the mean salary of students who get employed */\n",
    "/* based on each categorical variable. */\n",
    "/* Macro usage */\n",
    "\n",
    "ods graphics;\n",
    "\n",
    "%let category = gender hsc_s degree_t workexp specialisation;\n",
    "\n",
    "%macro oneway(response=, predictor=);\n",
    "\n",
    "%let i = 1;\n",
    "%do %while(%scan(&predictor,&i,%str( )) ^= %str());\n",
    "\n",
    "\t\t%let factor = %scan(&predictor,&i,%str( ));\n",
    "\t\t\n",
    "\t\tproc glm data=project.placement plots=diagnostics;\n",
    "\t\t\twhere &response ne .;\n",
    "\t\t\tclass &factor;\n",
    "\t\t\tmodel &response = &factor;\n",
    "\t\t\tmeans &factor / hovtest=levene;\n",
    "\t\t\ttitle \"One-Way ANOVA with &factor as Predictor\";\n",
    "\n",
    "\t\t%let i = %eval(&i + 1);\n",
    "%end;\n",
    "\t\n",
    "run;\n",
    "quit;\n",
    "title;\n",
    "\n",
    "%mend oneway;\n",
    "%oneway(response=salary, predictor=&category);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 04. Correlation Analysis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Producing correlation statistics and scatter plots */\n",
    "/* Part A */\n",
    "\n",
    "%let interval = ssc_p hsc_p degree_p ptest_p mba_p;\n",
    "\n",
    "ods graphics / reset=all imagemap;\n",
    "proc corr data=project.placement \n",
    "\t\t\t   rank plots(only)=scatter(nvar=all ellipse=none);\n",
    "\twhere salary ne .;\n",
    "\tvar &interval;\n",
    "\twith salary;\n",
    "\tid sl_no;\n",
    "\ttitle \"Correlations and Scatter Plots with Salary\";\n",
    "run;\n",
    "quit;\n",
    "title;\n",
    "\n",
    "\n",
    "/* Production correlation statistics among potential predictor variables */\n",
    "/* Part B */\n",
    "\n",
    "ods graphics off;\n",
    "proc corr data=project.placement nosimple best=3;\n",
    "\twhere salary ne .;\n",
    "\tvar &interval;\n",
    "\ttitle \"Correlations and Scatter Plot Matrix of Predictors\";\n",
    "run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 05. Simple Linear Regression"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Performing simple linear regression model */\n",
    "/* using each continuous variables as predictor and salary as response */\n",
    "/* Part A */\n",
    "\n",
    "%let categorical=ssc_p hsc_p degree_p ptest_p mba_p;\n",
    "\n",
    "%macro simple(response=, predictor=);\n",
    "\n",
    "%let i = 1;\n",
    "%do %while(%scan(&predictor,&i,%str( )) ^= %str( ));\n",
    "\n",
    "\t%let var = %scan(&predictor,&i,%str( ));\n",
    "\t\n",
    "\tproc reg data=project.placement;\n",
    "\t\twhere &response ne .;\n",
    "\t\tmodel &response = &var;\n",
    "\t\ttitle \"Simple Regression with &var as Regressor\";\n",
    "\t\n",
    "\t%let i = %eval(&i + 1);\n",
    "%end;\n",
    "\n",
    "run;\n",
    "title;\n",
    "\n",
    "%mend simple;\n",
    "%simple(response=salary, predictor=&categorical);\n",
    "\n",
    "\n",
    "/* Performing simple linear regression model*/\n",
    "/* using mba_p as predictor and salary as response */\n",
    "/* Part B */\n",
    "\n",
    "ods graphics;\n",
    "\n",
    "proc reg data=project.placement;\n",
    "\twhere salary ne .;\n",
    "\tmodel salary=mba_p;\n",
    "\ttitle \"Simple Regression with MBA_P as Regressor\";\n",
    "run;\n",
    "quit;\n",
    "title;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 06. Categorical Analysis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Examing the distribution of categorical variables; */\n",
    "/* gender, work experience, and degree type */\n",
    "/* with response variable status */\n",
    "/* Part A */\n",
    "\n",
    "proc freq data=project.placement;\n",
    "\ttables status gender workexp degree_t\n",
    "\t\t   gender*status workexp*status degree_t*status /\n",
    "\t\t   plots(only)=freqplot(scale=percent);\n",
    "run;\n",
    "\n",
    "\n",
    "/* Examing the distribution of continuous variables; */\n",
    "/* placement percentage, degree percentage, and MBA percenetage */\n",
    "/* with response variable status */\n",
    "/* Part B */\n",
    "\n",
    "proc univariate data=project.placement noprint;\n",
    "\tclass status;\n",
    "\tvar ptest_p degree_p mba_p;\n",
    "\thistogram ptest_p degree_p mba_p;\n",
    "\tinset mean std median min max / format=5.2 position=ne;\n",
    "run;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 07. Tests of Associations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Conducting a formal test to determine */\n",
    "/* whehter the associations are significant */\n",
    "\n",
    "ods graphics off;\n",
    "proc freq data=project.placement;\n",
    "\ttables (workexp gender)*status /\n",
    "\t\t\tchisq expected cellchi2 nocol nopercent relrisk;\n",
    "\ttitle \"Associations with Status\";\n",
    "run;\n",
    "title;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 08. Fitting Logistic Regression"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Fitting a binary Logistic regression model */\n",
    "/* to characterize the relationship between */\n",
    "/* variables, Status and Degree percentage */\n",
    "\n",
    "proc format;\n",
    "\tvalue $job 'Placed' = 1 \n",
    "\t\t\t   'Not Placed' = 0 ;\n",
    "run;\n",
    "\n",
    "ods graphics on;\n",
    "\n",
    "proc logistic data=project.placement alpha=0.05\n",
    "\t\t\t  plots(only)=(effect oddsratio);\n",
    " \tmodel Status(event='1') = Degree_P / clodds=pl;\n",
    " \tformat Status $job.; \n",
    "    title \"LOGISTIC MODEL (1): Status = Degree_P\";\n",
    "run;\n",
    "title;\n",
    "\n",
    "ods graphics off;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 09. Fitting Multiple Logistic Regression"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Fitting a multiple logistic regression model */\n",
    "/* to characterize the relationship between */\n",
    "/* variables, Status and Degree_P, Gender, and WorkExp */\n",
    "/* Part A */\n",
    "\n",
    "proc format;\n",
    "\tvalue $job 'Placed' = 1\n",
    "\t\t\t   'Not Placed' = 0;\n",
    "run;\n",
    "\n",
    "ods graphics on;\n",
    "\n",
    "proc logistic data=project.placement plots(only)=(effect oddsratio);\n",
    "\tclass Gender(ref='F') WorkExp(ref='No') / param=ref;\n",
    "\tmodel Status(event='1') = Degree_P Gender WorkExp / clodds=pl;\n",
    "\tunits Degree_P=10;\n",
    "\tformat Status $job.;\n",
    "\ttitle \"LOGISTIC MODEL (2): Status = Degree_P Gender WorkExp\";\n",
    "run;\n",
    "title;\n",
    "\n",
    "ods graphics off;\n",
    "\n",
    "\n",
    "/* Fitting a multiple logistic regression model */\n",
    "/* with interactions between predictor variables */\n",
    "/* Part B */\n",
    "\n",
    "proc format;\n",
    "\tvalue $job 'Placed' = 1\n",
    "\t\t\t   'Not Placed' = 0;\n",
    "run;\n",
    "\n",
    "ods graphics on;\n",
    "\n",
    "proc logistic data=project.placement plots(only)=(effect oddsratio);\n",
    "\tclass Gender(ref='F') WorkExp(ref='No') / param=ref;\n",
    "\tmodel Status(event='1') = Degree_P|Gender|WorkExp @2 /\n",
    "\t\t  selection=backward clodds=pl slstay=0.10;\n",
    "\tunits Degree_P=10;\n",
    "\tformat Status $job.;\n",
    "\ttitle \"LOGISTIC MODEL (3): Backward Elimination\"\n",
    "\t\t  \"Status = Degree_P|Gender|WorkExp\";\n",
    "run;\n",
    "title;\n",
    "\n",
    "ods graphics off;\n",
    "\n",
    "\n",
    "/* Fitting a multiple logistic regression model */\n",
    "/* with all odds ratio for each value of the variables */\n",
    "/* that are involved in an interaction form the final model */\n",
    "/* Part C */\n",
    "\n",
    "Proc format;\n",
    "\tvalue $job 'Placed' = 1\n",
    "\t\t\t   'Not Placed' = 0;\n",
    "run;\n",
    "\n",
    "ods graphics on;\n",
    "\n",
    "proc logistic data=project.placement plots(only)=oddsratio(range=clip);\n",
    "\tclass Gender(ref='F') WorkExp(ref='No') / param=ref;\n",
    "\tmodel Status(event='1') = Degree_P|Gender WorkExp;\n",
    "\tunits Degree_P=10;\n",
    "\toddsratio Degree_P / at (Gender=all) cl=pl;\n",
    "\toddsratio Gender / at (Degree_P= 50 70) cl=pl;\n",
    "\tformat Status $job.;\n",
    "\ttitle 'LOGISTIC MODEL (3.1): Status = Degree_p|Gender WorkExp';\n",
    "run;\n",
    "title;\n",
    "\n",
    "ods graphics off;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### 10. Generating Predictions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "/* Generating Predictions */\n",
    "proc format;\n",
    "\tvalue $job 'Placed' = 1\n",
    "\t\t\t   'Not Placed' = 0;\n",
    "run;\n",
    "\n",
    "ods select none;\n",
    "\n",
    "proc logistic data=project.placement;\n",
    "\tclass Gender(ref='F') WorkExp(ref='No') / param=ref;\n",
    "\tmodel Status(event='1') = Degree_P|Gender WorkExp;\n",
    "\tunits Degree_P=10;\n",
    "\tformat Status $job.;\n",
    "\t\tstore out=joboffer;\n",
    "run;\n",
    "\n",
    "ods select all;\n",
    "\n",
    "\n",
    "/* Input new data */\n",
    "data newcandid;\t\n",
    "\tlength Gender $3 WorkExp $3;\n",
    "\tinput Degree_P Gender $ WorkExp $;\n",
    "\tdatalines;\n",
    "\t51 F Yes\n",
    "\t49 M Yes\n",
    "\t68 F No\n",
    "\t63 M No\n",
    "\t82 F Yes\n",
    "\t76 M Yes\n",
    "\t79 M No\n",
    "\t87 F No\n",
    "\t;\n",
    "run;\n",
    "\n",
    "\n",
    "/* Score the new data */\n",
    "proc plm restore=joboffer;\n",
    "\tscore data=newcandid out=scored_candid / ILINK;\n",
    "\ttitle 'Predictions using PROC PLM';\n",
    "run;\n",
    "\n",
    "proc print data=scored_candid;\n",
    "run;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SAS",
   "language": "sas",
   "name": "sas"
  },
  "language_info": {
   "codemirror_mode": "sas",
   "file_extension": ".sas",
   "mimetype": "text/x-sas",
   "name": "sas"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
